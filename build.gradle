plugins {
    id 'idea'
    id 'groovy'
    id 'codenarc'
}

group = 'playground'
version = '1.0'
description = 'This gradle test automation project is built for quick start of api testing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

ext {
    groovyVersion = '2.4.11'
    cucumberVersion = '2.0.1'
    cucumberReportVersion = '1.1'
    junitVersion = '4.12'
    hamcrestVersion = '2.0.0.0'
    wsliteVersion = '1.1.3'
    wireMockVersion = '2.7.1'
    slf4jVersion = '1.7.25'
    snakeyamlVersion = '1.18'
}

dependencies {
    // Groovy
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"

    // Cucumber, Cucumber JUnit and Cucumber Report
    compile "io.cucumber:cucumber-groovy:$cucumberVersion"
    testCompile "io.cucumber:cucumber-junit:$cucumberVersion"
    compile "com.github.mkolisnyk:cucumber-runner:$cucumberReportVersion"

    // JUnit and Hamcrest
    testCompile "junit:junit:$junitVersion"
    testCompile "org.hamcrest:hamcrest-junit:$hamcrestVersion"

    // wslite
    compile "com.github.groovy-wslite:groovy-wslite:$wsliteVersion"

    // Wiremock
    compile "com.github.tomakehurst:wiremock:$wireMockVersion"

    // Simple Log4j
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    testCompile "org.slf4j:slf4j-simple:$slf4jVersion"

    // Snakeyaml
    compile "org.yaml:snakeyaml:$snakeyamlVersion"
}

sourceSets {
    cucumberTest {
        groovy {
            srcDirs = ['src/cucumberTest/groovy']
        }
        resources {
            srcDirs = ['src/cucumberTest/resources']
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    cucumberTestCompile.extendsFrom testCompile
    cucumberTestRunTime.extendsFrom testRunTime
}

defaultTasks 'cucumberTest'

task cucumberTest(type: Test, dependsOn: cleanTest) {
    description 'Runs all cucumber tests through Cucumber JUnit'

    // bypass up-to-date check to run tests every time
    outputs.upToDateWhen { false }

    // setup test dir and classpath
    setTestClassesDirs sourceSets.cucumberTest.output.classesDirs
    classpath = sourceSets.cucumberTest.runtimeClasspath

    // set a system property for the test JVM(s) - for cucumber options
    systemProperty 'cucumber.options', System.properties.getProperty('cucumber.options')

    // set up basic slf4j properties
    systemProperty 'org.slf4j.simpleLogger.showDateTime', 'true'
    systemProperty 'org.slf4j.simpleLogger.dateTimeFormat', 'yyyy-MM-dd\'T\'HH:mm:ss'

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // disable default html report
    reports {
        html.enabled = false
    }

    // generates cucumber report
    finalizedBy 'cucumberReport'
}

task cucumberReport(type: JavaExec) {
    description 'Executes groovy script to create customized cucumber reports'

    classpath = sourceSets.main.runtimeClasspath
    main = 'utility/ReportGeneration'
}

// Codenarc - Execute Groovy code static analysis
codenarcMain {
    configFile file('src/test/resources/codenarc.groovy')
    ignoreFailures true
}

codenarcTest {
    configFile file('src/test/resources/codenarc.groovy')
    ignoreFailures true
}

codenarcCucumberTest {
    configFile file('src/test/resources/codenarc.groovy')
    ignoreFailures true
}
